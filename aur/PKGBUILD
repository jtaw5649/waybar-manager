# Maintainer: jtaw5649 <213313463+jtaw5649@users.noreply.github.com>
pkgname=barforge
pkgver=0.3.1
pkgrel=1
pkgdesc="Browse, install, and manage Waybar modules"
arch=('x86_64')
url="https://github.com/jtaw5649/barforge-app"
license=('GPL-3.0-or-later')
depends=(
    'dbus'
    'vulkan-icd-loader'
)
makedepends=(
    'cargo'
    'git'
)
optdepends=(
    'waybar: the status bar this manager configures'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('SKIP')

prepare() {
    cd "$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd "$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm755 "target/release/barforge" "$pkgdir/usr/bin/barforge"
    install -Dm644 "data/dev.barforge.Barforge.desktop" "$pkgdir/usr/share/applications/dev.barforge.Barforge.desktop"
    install -Dm644 "data/dev.barforge.Barforge.metainfo.xml" "$pkgdir/usr/share/metainfo/dev.barforge.Barforge.metainfo.xml"
    install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
